
Перем Таблица;

Процедура ПриСозданииОбъекта(ТекстСтраницы, Код)
	
	ИнициализацияТаблицы();
	
	СтрокаПроект = "<tr parent-group=""" + Код + """ style=(\s|.)+?</tr>";
	СовпаденияСтрокаПроект = Служебный.СовпаденияВТексте(ТекстСтраницы, СтрокаПроект);
	
	сч = 1;

	Для Каждого СовпадениеСтрокаПроект Из СовпаденияСтрокаПроект Цикл
		
		соотвДанные = ПолучитьДанныеПоСтроке(СовпадениеСтрокаПроект.Значение);

		стрПроект = Таблица.Добавить();
		стрПроект.Наименование	 = соотвДанные["0.nameColumn"];
		стрПроект.СсылкаПроекта	 = соотвДанные["0.Ref"];
		стрПроект.Релиз			 = соотвДанные["1.versionColumn actualVersionColumn"];
		стрПроект.СсылкаРелиза	 = соотвДанные["1.Ref"];
		стрПроект.Дата			 = соотвДанные["2.releaseDate"];
 
		стрПроект.ПланРелиз		 = соотвДанные["3.versionColumn"];
		стрПроект.ПланДата		 = соотвДанные["4.planReleaseDate"];
		стрПроект.ДатаОбновления = соотвДанные["5.updateDate"];
 
		стрПроект.ТестРелиз		 = соотвДанные["6.versionColumn"];
		стрПроект.ТестДата		 = соотвДанные["7.publicationDate"];

		стрПроект.НомерСтроки = сч;
		сч = сч + 1;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьТаблицу() Экспорт
	Возврат Таблица;
КонецФункции

Процедура ИнициализацияТаблицы()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("НомерСтроки");

	Таблица.Колонки.Добавить("Наименование");
	Таблица.Колонки.Добавить("СсылкаПроекта");
	Таблица.Колонки.Добавить("Релиз");
	Таблица.Колонки.Добавить("СсылкаРелиза");
	Таблица.Колонки.Добавить("Дата");

	Таблица.Колонки.Добавить("ПланРелиз");
	Таблица.Колонки.Добавить("ПланДата");
	Таблица.Колонки.Добавить("ДатаОбновления");

	Таблица.Колонки.Добавить("ТестРелиз");
	Таблица.Колонки.Добавить("ТестДата");
		
КонецПроцедуры

Функция ПолучитьДанныеПоСтроке(Текст)

	Выражение = "<td((\s|.)*?)</td>";

	Совпадения = Служебный.СовпаденияВТексте(Текст, Выражение);
	Если НЕ ЗначениеЗаполнено(Совпадения) Тогда
		ВызватьИсключение "Тут что-то должно быть обязательно";
	КонецЕсли;
	соотв = Новый Соответствие;

	сч = 0;
	Для Каждого Совпадение Из Совпадения Цикл
		КЗ = Данные_td(Совпадение.Значение);
		соотв.Вставить(Строка(сч) + "." + КЗ.Ключ, КЗ.Значение);
		соотв.Вставить(Строка(сч) + ".Ref", КЗ.Ссылка);
		сч = сч + 1;	
	КонецЦикла;
	
	Возврат соотв;

КонецФункции

Функция Данные_td(Знач Текст)

	Совпадения = Служебный.СовпаденияВТексте(Текст, "class=""(.+?)""");
	Имя = Совпадения[0].Группы[1].Значение;

	Совпадения = Служебный.СовпаденияВТексте(Текст, "<a href=""(.+?)"">");
	Если Совпадения.Количество() Тогда
		Ссылка = Совпадения[0].Группы[1].Значение;
	Иначе
		Ссылка = "";
	КонецЕсли;

	РВ = Новый РегулярноеВыражение("<.+?>");
	Описание = СокрЛП(РВ.Заменить(Текст, ""));
	стрОписание = СтрРазделить(Описание, Символы.ПС, Ложь);
	Если стрОписание.Количество() > 1 Тогда
		массив = Новый Массив();
		Для Каждого стр Из стрОписание Цикл
			Если ПустаяСтрока(стр) Тогда
				Продолжить;
			КонецЕсли;
			массив.Добавить(СокрЛП(стр));
		КонецЦикла;
		Описание = массив;
	КонецЕсли;
	
	Возврат Новый Структура("Ключ,Значение,Ссылка", Имя, Описание, Ссылка);
	
КонецФункции
